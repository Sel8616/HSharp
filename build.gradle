buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'com.jfrog.bintray' version '1.7.1'
}

group 'cn.sel'
version '0.7'

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: "com.jfrog.bintray"

repositories {
    jcenter()
}

dependencies {
    compile 'org.slf4j:slf4j-api:+',
            'javax.activation:activation:+'
    testCompile "javax.servlet:javax.servlet-api:3.1.0",
            "org.eclipse.jetty:jetty-webapp:+",
            "org.slf4j:slf4j-jdk14:+"

}

def pomPath = "$buildDir/classes/main/META-INF/maven/$project.group/$project.name/"

task generatePomProperties(dependsOn: 'generatePomFileForMavenPublication') {
    Properties pomProperties = new Properties()
    pomProperties.setProperty('groupId', project.group)
    pomProperties.setProperty('artifactId', project.name)
    pomProperties.setProperty('version', project.version)
    def dir = file(pomPath)
    if (!dir.exists()) dir.mkdirs()
    pomProperties.store(file(pomPath + 'pom.properties').newOutputStream(), project.group + ':' + project.name + ':' + project.version)
}

task copyLicense(type: Copy) {
    from "$rootDir"
    into "$buildDir/classes/main"
    include 'LICENSE'
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

jar.dependsOn(generatePomProperties, copyLicense)

bintray {
    Properties properties = new Properties()
    properties.load(new FileInputStream("${System.properties['user.home']}/.bintray/bintray.properties"))
    user = properties.getProperty('BINTRAY_USERNAME')
    key = properties.getProperty('BINTRAY_API_KEY')
    pkg {
        publish = true
        repo = 'maven'
        name = project.name
        version {
            name = project.version
        }
    }
    publications = ['maven']
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId project.group
            artifactId project.name
            version project.version
            from components.java
            artifact sourceJar
        }
    }
}

model {
    tasks.generatePomFileForMavenPublication {
        destination = file(pomPath + 'pom.xml')
    }
}